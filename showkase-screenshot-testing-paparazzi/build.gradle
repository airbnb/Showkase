plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
}
apply from: "$rootDir/publishing.gradle"

// Added to resolve this error
// /home/runner/work/Showkase/Showkase/showkase-screenshot-testing-paparazzi/build.gradle:
// Error: commons-logging defines classes that conflict with classes now provided by Android.
// Solutions include finding newer versions or alternative libraries that don't have the same
// problem (for example, for httpclient use HttpUrlConnection or okhttp instead), or repackaging
// the library using something like jarjar. [DuplicatePlatformClasses]
configurations {
    all {
        exclude module: 'httpclient'
        exclude module: 'commons-logging'
    }
}

android {
    namespace 'com.airbnb.android.showkase.screenshot.testing.paparazzi'
    compileSdk 32

    defaultConfig {
        minSdk 21
        targetSdk 32
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
        freeCompilerArgs += [
                '-Xopt-in=kotlin.RequiresOptIn',
        ]
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerVersion "${versions.kotlinCompilerVersion}"
        kotlinCompilerExtensionVersion "${versions.composeCompiler}"
    }
}

dependencies {
    // Showkase
    api project(':showkase')
    api deps.test.paparazzi
    api deps.compose.foundation
    api deps.compose.activityCompose

    // Testing
    api deps.test.testParameterInjector
    api deps.test.androidXTestRules
    api deps.test.androidxTestRunner
}