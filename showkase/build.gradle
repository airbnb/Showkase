buildscript {
    repositories {
        mavenCentral()
    }
    // Maven artifact publish
    dependencies {
        classpath 'com.vanniktech:gradle-maven-publish-plugin:0.15.1'
    }
}

plugins {
    id 'com.android.library'
    id 'kotlin-android'
}
apply plugin: "com.vanniktech.maven.publish"

android {
    compileSdkVersion 31

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
        freeCompilerArgs += [
                '-Xopt-in=kotlin.RequiresOptIn',
        ]
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerVersion "${versions.kotlinCompilerVersion}"
        kotlinCompilerExtensionVersion "${versions.compose}"
    }
    // Added to avoid this error -
    // Execution failed for task ':app:mergeDebugAndroidTestJavaResource'.
    // > A failure occurred while executing com.android.build.gradle.internal.tasks.MergeJavaResWorkAction
    // > 2 files found with path 'META-INF/AL2.0' from inputs:
    packagingOptions {
        exclude 'META-INF/AL2.0'
        exclude 'META-INF/LGPL2.1'
    }
}

dependencies {
    // Showkase
    api project(':showkase-annotation')

    // Support Libraries
    implementation deps.support.appCompat
    implementation deps.support.ktx

    // Compose
    implementation deps.compose.activityCompose
    implementation deps.compose.composeRuntime
    implementation deps.compose.composeNavigation
    implementation deps.compose.core
    implementation deps.compose.foundation
    implementation deps.compose.layout
    implementation deps.compose.material
    androidTestImplementation deps.compose.uiTest

    // Unit Testing
    testImplementation deps.test.junit
    androidTestImplementation deps.test.junitImplementation
}
