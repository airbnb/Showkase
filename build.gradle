// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.KSP_VERSION = '1.6.10-1.0.4'

    ext.versions = [
            'androidXTestCore':'1.4.0',
            'androidXTestRules':'1.4.0',
            'assertJ': '3.16.1',
            'compose': '1.1.1',
            'composeActivity': '1.4.0',
            'composeConstraintLayout': '1.0.0',
            'composeNavigation': '2.4.1',
            'detekt': '1.7.4',
            'espresso': '3.2.0',
            'gradle': '7.0.3',
            'junit' : '4.13',
            'junitImplementation' : '1.1.2',
            'kotlin': '1.6.10',
            'kotlinCompilerVersion': '1.6.10',
            'kotlinCompileTesting': '1.4.7',
            'kotlinPoet': '1.10.2',
            'kotlinXMetadata': '0.4.0',
            'ksp': "$KSP_VERSION",
            'ktx': '1.1.0',
            'lifecycle':'2.2.0',
            'picasso': '2.8',
            'appcompat': '1.4.0',
            'testRunner': '1.4.0',
            'testParameterInjector': '1.8',
            'googleTruth': '1.1.3',
            'material':'1.4.0',
            'mdcComposeThemeAdapter':'1.0.2',
            'strikt':'0.33.0',
            'xprocessing':'2.4.2',
            'corektx':'1.7.0',
            'shot': '5.13.0',
    ]
    ext.deps = [
            'compose': [
                    'activityCompose': "androidx.activity:activity-compose:${versions.composeActivity}",
                    'composeComplier': "androidx.compose:compose-compiler:${versions.compose}",
                    'composeNavigation': "androidx.navigation:navigation-compose:${versions.composeNavigation}",
                    'composeRuntime': "androidx.compose.runtime:runtime:${versions.compose}",
                    'constraintLayout': "androidx.constraintlayout:constraintlayout-compose:${versions.composeConstraintLayout}",
                    'core': "androidx.compose.ui:ui:${versions.compose}",
                    'foundation': "androidx.compose.foundation:foundation:${versions.compose}",
                    'tooling': "androidx.compose.ui:ui-tooling:${versions.compose}",
                    'toolingPreview': "androidx.compose.ui:ui-tooling-preview:${versions.compose}",
                    'layout': "androidx.compose.foundation:foundation-layout:${versions.compose}",
                    'material': "androidx.compose.material:material:${versions.compose}",
                    'savedInstanceState': "androidx.compose.runtime:runtime-saveable:${versions.compose}",
                    'uiTest': "androidx.compose.ui:ui-test-junit4:${versions.compose}",
                    'uiLiveData': "androidx.compose.runtime:runtime-livedata:${versions.compose}"
            ],
            'kotlinCompileTesting': "com.github.tschuchortdev:kotlin-compile-testing:${versions.kotlinCompileTesting}",
            'kotlinCompileTestingKsp': "com.github.tschuchortdev:kotlin-compile-testing-ksp:${versions.kotlinCompileTesting}",
            'kotlinPoet': "com.squareup:kotlinpoet:${versions.kotlinPoet}",
            'kotlinJavaPoetInterop': "com.squareup:kotlinpoet-javapoet:${versions.kotlinPoet}",
            'kotlinXMetadata': "org.jetbrains.kotlinx:kotlinx-metadata-jvm:${versions.kotlinXMetadata}",
            'ksp': "com.google.devtools.ksp:symbol-processing-api:${versions.ksp}",
            'imageLoading': [
                    'picasso': "com.squareup.picasso:picasso:${versions.picasso}"
            ],
            'support': [
                    'appCompat': "androidx.appcompat:appcompat:${versions.appcompat}",
                    'ktx': "androidx.core:core-ktx:${versions.corektx}",
                    'lifecycleExtensions': "androidx.lifecycle:lifecycle-extensions:${versions.lifecycle}"
            ],
            'test': [
                    'androidXTestCore':"androidx.test:core:${versions.androidXTestCore}",
                    'androidXTestRules':"androidx.test:rules:${versions.androidXTestRules}",
                    'assertJ': "org.assertj:assertj-core:${versions.assertJ}",
                    'googleTruth': "com.google.truth:truth:${versions.googleTruth}",
                    'junit': "junit:junit:${versions.junit}",
                    'junitImplementation': "androidx.test.ext:junit:${versions.junitImplementation}",
                    'androidxTestRunner': "androidx.test:runner:${versions.testRunner}",
                    'strikt':"io.strikt:strikt-core:${versions.strikt}",
                    'shotAndroid': "com.karumi:shot-android:${versions.shot}",
                    'testParameterInjector': "com.google.testparameterinjector:test-parameter-injector:${versions.testParameterInjector}"
            ],
            'material': [
                    'material':"com.google.android.material:material:${versions.material}",
                    'mdcComposeThemeAdapter':"com.google.android.material:compose-theme-adapter:${versions.mdcComposeThemeAdapter}"
            ],
            'xprocessing': "androidx.room:room-compiler-processing:${versions.xprocessing}",
            'xprocessingTesting': "androidx.room:room-compiler-processing-testing:${versions.xprocessing}",
    ]
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:${versions.gradle}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "com.karumi:shot:${versions.shot}"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "1.7.4"
    id "com.google.devtools.ksp" version "$KSP_VERSION"
}

allprojects {
    apply from: "$rootDir/detekt/detekt.gradle"
    repositories {
        google()
        mavenCentral()
        jcenter()
    }
}

subprojects {
    afterEvaluate {
        /**
         * KSP does not currently register kotlin generated sources.
         * https://github.com/google/ksp/issues/37
         */
        if (project.extensions.findByType(com.android.build.gradle.LibraryExtension.class) != null) {
            project.android.libraryVariants.all { variant ->
                def variantName = variant.name
                def outputFolder = new File("build/generated/ksp/$variantName/kotlin")
                variant.addJavaSourceFoldersToModel(outputFolder)
                android.sourceSets.getAt(variantName).java {
                    srcDir(outputFolder)
                }

                // Register the generated unit test sources as well, since Scabbard generates trio unit tests with KSP,
                // and we can't run them from the IDE unless they're registered.

                // Note, there is a nuanced different between the name of the directory on disk and the source set name
                // in AGP.
                // eg "debugUnitTest"
                def testDirectoryName = "${variantName}UnitTest"
                // eg "testDebug"
                def testSourceSetName = "test${variantName.capitalize()}"

                // Not every module will have test sources, so finding it is optional.
                android.sourceSets.findByName(testSourceSetName)?.kotlin {
                    def testOutputFolder = new File("build/generated/ksp/$testDirectoryName/kotlin")
                    srcDir(testOutputFolder)
                }
            }
        } else if (project.extensions.findByType(com.android.build.gradle.AbstractAppExtension.class) != null) {
            project.android.applicationVariants.all { variant ->
                def variantName = variant.name
                def outputFolder = new File("build/generated/ksp/$variantName/kotlin")
                variant.addJavaSourceFoldersToModel(outputFolder)
                android.sourceSets.getAt(variantName).java {
                    srcDir(outputFolder)
                }

                // Register the generated unit test sources as well, since Scabbard generates trio unit tests with KSP,
                // and we can't run them from the IDE unless they're registered.

                // Note, there is a nuanced different between the name of the directory on disk and the source set name
                // in AGP.
                // eg "debugUnitTest"
                def testDirectoryName = "${variantName}UnitTest"
                // eg "testDebug"
                def testSourceSetName = "test${variantName.capitalize()}"

                // Not every module will have test sources, so finding it is optional.
                android.sourceSets.findByName(testSourceSetName)?.kotlin {
                    def testOutputFolder = new File("build/generated/ksp/$testDirectoryName/kotlin")
                    srcDir(testOutputFolder)
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
